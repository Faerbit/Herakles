/*
 * Herakles - Sports Activity Tracking App for Android
 * Copyright (c) 2017 Fabian Klemp
 *
 * You should have received a copy of the GNU General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

def computeVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--count', 'HEAD'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

ext.computeVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")

def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    project.setProperty("keyFile")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def renameApk(variant, defaultConfig) {
    variant.outputs.all { output ->
        def buildType = ""
        def file = output.outputFile
        if (file.name.contains("release")) {
            buildType = "release"
        } else {
            buildType = "debug"
        }
        def fileName = ""
        if (buildType == "debug") {
            fileName = defaultConfig.applicationId + "-"
            fileName += computeVersionName() + "-"
            fileName += buildType + ".apk"
        }
        else {
            fileName = defaultConfig.applicationId + "-"
            fileName += defaultConfig.versionName + "-"
            fileName += buildType + ".apk"
        }
        output.outputFileName = fileName
    }
}

android {
    signingConfigs {
        if (project.hasProperty("keyFile")) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
        else {
            release {}
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "it.faerb.herakles"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 2
        versionName "v0.1.1"
        versionCode computeVersionCode()
        versionName computeVersionName()
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [applicationLabel: "@string/app_name_debug"]
            applicationVariants.all { variant ->
                renameApk(variant, defaultConfig)
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [applicationLabel: "@string/app_name"]
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                renameApk(variant, defaultConfig)
            }
        }
    }
    applicationVariants.all { variant ->
        def flavor = variant.mergedFlavor
        if (variant.buildType.isDebuggable()) {
        flavor.versionName = computeVersionName()
        flavor.versionCode = computeVersionCode()
        }
        else {
            flavor.versionName = defaultConfig.versionName
            flavor.versionCode = defaultConfig.versionCode
        }
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'org.osmdroid:osmdroid-android:5.6.4'
    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.android.support:recyclerview-v7:27.0.2'
    compile 'com.google.code.gson:gson:2.8.0'
}
